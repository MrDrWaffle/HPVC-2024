#include <LiquidCrystal_I2C.h>

#define CADENCE_PIN 2
#define SPEED_PIN 3
//cm
#define WHEEL_CIRCUMFERENCE 30 

const int updateInterval = 3000;
unsigned long previousUpdateTime = 0;

//cadence variables
const int cadenceInterval = 3000; 
const int numCadenceReadings = 3; 
unsigned long previousCadenceTime = 0;
int pedalRevolutions = 0;
float cadence = 0.0;
float cadenceReadings[numCadenceReadings] = {0.0}; 
int cadenceIndex = 0; 

//speed variables
const int speedInterval = 3000; 
const int numSpeedReadings = 3; 
unsigned long previousSpeedTime = 0;
int wheelRevolutions = 0;
float speed = 0.0;
float speedReadings[numSpeedReadings] = {0.0}; 
int speedIndex = 0; 
//Define lcd obj
LiquidCrystal_I2C lcd(0x20, 16, 2); // I2C address 0x27, 16 column and 2 rows


void setup() {
  Serial.begin(9600);
  
  lcd.init(); //initialize the lcd
  lcd.backlight(); //open the backlight 
  
  lcd.setCursor(0, 0);
  lcd.print("INITIALIZING");
  delay(1000);
  lcd.clear();

  
  pinMode(CADENCE_PIN, INPUT);
  attachInterrupt(digitalPinToInterrupt(CADENCE_PIN), cadenceSensorISR, RISING);
  
  pinMode(SPEED_PIN, INPUT);
  attachInterrupt(digitalPinToInterrupt(SPEED_PIN), speedSensorISR, RISING);
}

void loop() {
  unsigned long currentTime = millis();
  float cadenceResult;
  float speedResult;
	
  cadenceResult = measureCadence(currentTime);
  speedResult = measureSpeed(currentTime);
  


  updateValues(currentTime, cadenceResult, speedResult);

}

float measureCadence(unsigned long currentTime) {
  if (currentTime - previousCadenceTime >= cadenceInterval) {
    
    cadence = (pedalRevolutions * 60000.0) / (cadenceInterval);
    //Serial.print(cadence);
    pedalRevolutions = 0;
    
    //place in stack
    cadenceReadings[cadenceIndex] = cadence;
    cadenceIndex = (cadenceIndex + 1) % numCadenceReadings;
    
    previousCadenceTime = currentTime;
  }
  
  float avgCadence = 0.0;
  for (int i = 0; i < numCadenceReadings; ++i) {
    avgCadence += cadenceReadings[i];
  }
  avgCadence /= numCadenceReadings;
  
  return avgCadence; 
}

float measureSpeed(unsigned long currentTime) {
  if (currentTime - previousSpeedTime >= speedInterval) {
    
    speed = (WHEEL_CIRCUMFERENCE * wheelRevolutions) / (speedInterval / 1000.0);
    speed = speed * 0.036; //convert to km/h
    //Serial.println(speed);
    wheelRevolutions = 0;
    
    //place in stack
    speedReadings[speedIndex] = speed;
    speedIndex = (speedIndex + 1) % numSpeedReadings;
    
    previousSpeedTime = currentTime;
  }
  
  float avgSpeed = 0.0;
  for (int i = 0; i < numSpeedReadings; ++i) {
    avgSpeed += speedReadings[i];
  }
  avgSpeed /= numSpeedReadings;
  
  return avgSpeed;
  
}

void updateValues(unsigned long currentTime, float cadenceResult, float speedResult) {
  if (currentTime - previousUpdateTime >= updateInterval) {

    Serial.print("Current Cadence: ");
    Serial.print(cadenceResult);
    Serial.println(" RPM");
    
    Serial.print("Current Speed: ");
    Serial.print(speedResult);
    Serial.println(" km/h");
    
    lcd.clear();
    lcd.setCursor(0, 0);
  	lcd.print("Cadence: " + String(cadenceResult, 2) + " RPM");
    lcd.setCursor(0, 1);
    lcd.print("Speed: " + String(speedResult, 2) + " km/h");

    
    previousUpdateTime = currentTime;
  }
}

void cadenceSensorISR() {
  pedalRevolutions++;
}

void speedSensorISR() {
  wheelRevolutions++;
}
